buildscript {

    ext {
        support_version = '28.0.0'
        kotlin_version = '1.3.31'
    }

    repositories {
        google()
        jcenter()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"

        classpath 'com.android.tools.build:gradle:3.6.2'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.21.0'
        classpath 'de.mobilej.unmock:UnMockPlugin:0.7.0'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:2.0'
    }
}


subprojects {
    apply plugin: 'com.android.library'
    apply plugin: 'kotlin-android'
    apply plugin: 'android-maven'
    apply plugin: 'de.mobilej.unmock'
    apply plugin: 'com.github.ben-manes.versions'

    def libVersionName = '0.22'
    def libVersionCode = 22

    version libVersionName

    repositories {
        google()
        jcenter()
        maven { url 'https://maven.google.com' }
    }

    dependencies {
        implementation 'androidx.legacy:legacy-support-v4:1.0.0'

        implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"

        testImplementation 'junit:junit:4.12'
        testImplementation 'com.squareup.assertj:assertj-android:1.2.0'
        testImplementation "io.mockk:mockk:1.9.3"
    }

    android {
        compileSdkVersion 28

        defaultConfig {
            minSdkVersion 14
            targetSdkVersion 28
            versionCode libVersionCode
            versionName libVersionName
        }

        lintOptions {
            baseline file("../.ci/lint-baseline.xml")
            warning 'GradleDependency'
            warning 'SourceLockedOrientationActivity'
            checkAllWarnings true
            warningsAsErrors true
        }

        task androidSourcesJar(type: Jar) {
            from android.sourceSets.main.java.source
            classifier = 'sources'
        }

        artifacts {
            archives androidSourcesJar
        }


    }

}